# Copy current pixel
data modify storage transfer_pet_owner:image data.current.pixel_color set \
  from storage transfer_pet_owner:image data.current.image_data[0]
data modify storage transfer_pet_owner:image data.current.description_text set \
  from storage transfer_pet_owner:image data.current.description[0]

scoreboard players add $index.image_data transfer_pet_owner.image 1

# Calculate column
scoreboard players operation $index.column transfer_pet_owner.image = $index.image_data transfer_pet_owner.image
scoreboard players operation $index.column transfer_pet_owner.image %= $width transfer_pet_owner.image

# Calculate row
scoreboard players operation $index.row transfer_pet_owner.image = $index.image_data transfer_pet_owner.image
scoreboard players operation $index.row transfer_pet_owner.image /= $width transfer_pet_owner.image

# Append start spacing
execute if score $index.column transfer_pet_owner.image matches 1 run \
  function transfer_pet_owner:image/process/append/blank

# Append colored pixel
execute unless data storage transfer_pet_owner:image data.current{pixel_color: ''} run \
  function transfer_pet_owner:image/process/append/pixel
# Append background pixel
execute if data storage transfer_pet_owner:image data.current{pixel_color: ''} run \
  function transfer_pet_owner:image/process/append/background

# Append description
execute if score $index.column transfer_pet_owner.image matches 0 run \
    function transfer_pet_owner:image/process/image_end/description

# Remove first element from the image_data array
data remove storage transfer_pet_owner:image data.current.image_data[0]
# Loop over rows
execute if score $index.row transfer_pet_owner.image < $height transfer_pet_owner.image run \
  function transfer_pet_owner:image/process/loop_over_image_data
 